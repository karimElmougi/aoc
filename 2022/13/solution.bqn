#! /usr/bin/env BQN

⟨Partition⟩ ← •Import "../../utils.bqn"

input ← •FLines "input.txt"
# input ← ⟨
#     "[1,1,3,1,1]"
#     "[1,1,5,1,1]"
#     ""
#     "[[1],[2,3,4]]"
#     "[[1],4]"
#     ""
#     "[9]"
#     "[[8,7,6]]"
#     ""
#     "[[4,4],4,4]"
#     "[[4,4],4,4,4]"
#     ""
#     "[7,7,7,7]"
#     "[7,7,7]"
#     ""
#     "[]"
#     "[3]"
#     ""
#     "[[[]]]"
#     "[[]]"
#     ""
#     "[1,[2,[3,[4,[5,6,7]]]],8,9]"
#     "[1,[2,[3,[4,[5,6,0]]]],8,9]"
# ⟩
input ↩ •BQN¨˘ ¯1⊸↓˘  ⌽‿3 ⥊ {(⊐⟜𝕩 ⊏ (∾⟜"⟨⟩" ¯2⊸↓))∘(∧⍷) 𝕩}¨ input

F ← { ((0⊸≠)⊸/) (𝕨 Compare¨ 𝕩) }
Compare ← {
    (0‿0 ≡ =¨ 𝕨‿𝕩) ? × 𝕨-𝕩;
    (=⟜0)∘= 𝕩 ? 𝕨 Compare ⋈𝕩;
    (=⟜0)∘= 𝕨 ? (⋈𝕨) Compare 𝕩;
    𝕨 >○≠ 𝕩 ? c ← ((≠𝕩)↑𝕨) F 𝕩 ⋄ ⊑ c∾1;
    𝕨 <○≠ 𝕩 ? c ← 𝕨 F (≠𝕨)↑𝕩 ⋄ ⊑ c∾¯1;
    c ← 𝕨 F 𝕩 ⋄ ⊑ c∾0
}

•Show +´ 1 + / ¯1 = Compare´˘ input

p ← ⟨⟨2⟩⟩‿⟨⟨6⟩⟩ ∾ ⊏ 1‿∘ ⥊ input
•Show ×´ 1 + / (=⟜1∨=⟜0)¨ (⍒ +´˘ ¯1 = Compare⌜˜ p)
